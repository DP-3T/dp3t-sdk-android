/*
 * Created by Ubique Innovation AG
 * https://www.ubique.ch
 * Copyright (c) 2020. All rights reserved.
 */

plugins {
	id 'com.android.library'
	id 'maven-publish'
	id 'com.jfrog.bintray'
	id 'com.google.protobuf'
}

android {
	compileSdkVersion 29

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 29
		versionCode 10
		versionName "0.1.10"
		testInstrumentationRunnerArgument 'androidx.benchmark.suppressErrors', 'EMULATOR,LOW-BATTERY,ACTIVITY-MISSING,DEBUGGABLE,UNLOCKED,UNSUSTAINED-ACTIVITY-MISSING'
		testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"

		consumerProguardFiles 'consumer-rules.pro'

		buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
	}

	defaultPublishConfig "productionRelease"
	publishNonDefault true

	flavorDimensions "version"

	productFlavors {
		calibration {
		}
		production {
		}
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
}

protobuf {
	protoc {
		// You still need protoc like in the non-Android case
		artifact = 'com.google.protobuf:protoc:3.11.4'
	}
	plugins {
		javalite {
			// The codegen for lite comes as a separate artifact
			artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				// In most cases you don't need the full Java output if you use the lite output.
				remove java
			}
			task.plugins {
				javalite {}
			}
		}
	}
}

task androidSourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}
afterEvaluate {
	publishing {
		publications {
			"sdkRelease"(MavenPublication) {
				from components.productionRelease
				groupId 'org.dpppt'
				artifactId 'dp3t-sdk-android'
				version android.defaultConfig.versionName
				artifact androidSourcesJar
			}
			"sdkCalibration"(MavenPublication) {
				from components.calibrationRelease
				groupId 'org.dpppt'
				artifactId 'dp3t-sdk-android'
				version android.defaultConfig.versionName + "-calibration"
				artifact androidSourcesJar
			}
		}
	}
}
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")
	publications = ['sdkRelease', 'sdkCalibration']
	publish = true
	pkg {
		repo = 'dp3t-sdk-android'
		name = 'dp3t-sdk-android'
		userOrg = 'dp3t'
		licenses = ['MPL-2.0']
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation 'androidx.core:core:1.2.0'
	implementation "androidx.security:security-crypto:1.0.0-beta01"
	implementation 'androidx.work:work-runtime:2.3.4'

	implementation 'com.squareup.retrofit2:retrofit:2.6.2'
	implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
	implementation('com.squareup.retrofit2:converter-protobuf:2.6.2') {
		exclude group: 'com.google.protobuf', module: 'protobuf-java'
	}
	implementation 'com.google.protobuf:protobuf-lite:3.0.1'

	testImplementation 'junit:junit:4.12'

	androidTestImplementation "androidx.benchmark:benchmark-junit4:1.0.0"
	androidTestImplementation 'androidx.test.ext:junit:1.1.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
